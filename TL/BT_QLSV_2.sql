CREATE DATABASE BT_QLSV_2
GO

USE BT_QLSV_2
GO
USE QLSV
GO

CREATE TABLE SINHVIEN (
	MSSV VARCHAR(5) NOT NULL,
	TEN  NVARCHAR(50) NOT NULL,
	PHAINU BIT NOT NULL,
	DIACHI NVARCHAR(100) NOT NULL,
	DIENTHOAI VARCHAR(20),
	MAKHOA VARCHAR(10)
)

ALTER TABLE SINHVIEN
ALTER COLUMN MAKHOA VARCHAR(10) NOT NULL

CREATE TABLE KHOA (
	MAKHOA VARCHAR(10) NOT NULL,
	TENKHOA NVARCHAR(50) NOT NULL,
	SL_CBGD SMALLINT NOT NULL
)

CREATE TABLE GIAOVIEN (
	MAGV VARCHAR(5) NOT NULL,
	TENGV NVARCHAR(50) NOT NULL,
	MAKHOA VARCHAR(10) NOT NULL
)

CREATE TABLE MONHOC (
	MAMH VARCHAR(5) NOT NULL,
	TENMH NVARCHAR(50) NOT NULL,
	SOTC INT NOT NULL
)

CREATE TABLE GIANGDAY (
	MAKHOAHOC VARCHAR(5) NOT NULL,
	MAGV VARCHAR(5) NOT NULL,
	MAMH VARCHAR(5) NOT NULL,
	HOCKY SMALLINT NOT NULL,
	NAM INT NOT NULL
)

CREATE TABLE KETQUA (
	MSSV VARCHAR(5) NOT NULL,
	MAKHOAHOC VARCHAR(5) NOT NULL,
	DIEM DECIMAL(3, 1) NOT NULL
)

ALTER TABLE SINHVIEN
ADD CONSTRAINT PK_SINHVIEN PRIMARY KEY (MSSV)

ALTER TABLE SINHVIEN
ADD CONSTRAINT FK_SINHVIEN_KHOA FOREIGN KEY (MAKHOA) REFERENCES KHOA(MAKHOA)

ALTER TABLE KHOA
ADD CONSTRAINT PK_KHOA PRIMARY KEY (MAKHOA)

ALTER TABLE GIAOVIEN
ADD CONSTRAINT PK_GIAOVIEN PRIMARY KEY (MAGV)

ALTER TABLE GIAOVIEN
ADD CONSTRAINT FK_GIAOVIEN_KHOA FOREIGN KEY (MAKHOA) REFERENCES KHOA(MAKHOA)

ALTER TABLE MONHOC
ADD CONSTRAINT PK_MONHOC PRIMARY KEY (MAMH)

ALTER TABLE GIANGDAY
ADD CONSTRAINT PK_GIANGDAY PRIMARY KEY (MAKHOAHOC)

ALTER TABLE GIANGDAY
ADD CONSTRAINT FK_GIANGDAY_GIAOVIEN FOREIGN KEY(MAGV) REFERENCES GIAOVIEN(MAGV)

ALTER TABLE GIANGDAY
ADD CONSTRAINT FK_GIANGDAY_MONHOC FOREIGN KEY (MAMH) REFERENCES MONHOC(MAMH)

ALTER TABLE KETQUA
ADD CONSTRAINT PK_KETQUA PRIMARY KEY (MSSV, MAKHOAHOC)

ALTER TABLE KETQUA
ADD CONSTRAINT FK_KETQUA_SINHVIEN FOREIGN KEY (MSSV) REFERENCES SINHVIEN(MSSV)

ALTER TABLE KETQUA
ADD CONSTRAINT FK_KETQUA_GIANGDAY FOREIGN KEY (MAKHOAHOC) REFERENCES GIANGDAY(MAKHOAHOC)

INSERT INTO SINHVIEN
VALUES ('SV001', 'BUI THUY AN', 1, '223 TRAN HUNG DAO .HCM', '8132202', 'CNTT')
INSERT INTO SINHVIEN
VALUES ('SV002', 'NGUYEN THANH TUNG', 0, '140 CONG QUYNH .HCM', '8125678', 'CNTT')
INSERT INTO SINHVIEN
VALUES ('SV003', 'NGUYEN THANH LONG', 0, '112/4 CONG QUYNH .HCM', '0918345623', 'TOAN')
INSERT INTO SINHVIEN
VALUES ('SV004', 'HOANG THI HOA', 1, '90 NG VAN CU .HCM', '8320123', 'CNTT')
INSERT INTO SINHVIEN
VALUES ('SV005', 'TRAN HONG SON', 0, '54 CAO THANG .HANOI', '8345987', 'TOAN')


INSERT INTO MONHOC
VALUES ('CSDL', 'CO SO DU LIEU', 3)
INSERT INTO MONHOC
VALUES ('CTDL', 'CAU TRUC DU LIEU', 4)
INSERT INTO MONHOC
VALUES ('KTLT', 'KY THUAT LAP TRINH', 5)
INSERT INTO MONHOC
VALUES ('CWIN', 'LAP TRINH C TREN WINDOW', 4 )

INSERT INTO KHOA
VALUES ('CNTT', N'Công nghệ thông tin', 15)
INSERT INTO KHOA
VALUES ('TOAN', N'Toán', 20)
INSERT INTO KHOA
VALUES ('SINH', N'Sinh học', 7 )

INSERT INTO GIAOVIEN
VALUES ('GV001', 'PHAM THI THAO', 'CNTT')
INSERT INTO GIAOVIEN
VALUES ('GV002', 'LAM HOANG VU', 'TOAN')
INSERT INTO GIAOVIEN
VALUES ('GV003', 'TRAN VAN TIEN', 'CNTT')
INSERT INTO GIAOVIEN
VALUES ('GV004', 'HOANG VUONG', 'CNTT' )

INSERT INTO GIANGDAY
VALUES ('K1', 'GV001', 'CSDL', 1, 2001)
INSERT INTO GIANGDAY
VALUES ('K2', 'GV004', 'KTLT', 2, 2001)
INSERT INTO GIANGDAY
VALUES ('K3', 'GV003', 'CTDL', 1, 2002)
INSERT INTO GIANGDAY
VALUES ('K4', 'GV004', 'CWIN', 1, 2002)
INSERT INTO GIANGDAY
VALUES ('K5', 'GV001', 'CSDL', 1, 2002)


INSERT INTO KETQUA
VALUES ('SV001', 'K1', 8.5)
INSERT INTO KETQUA
VALUES ('SV002', 'K3', 7.0)
INSERT INTO KETQUA
VALUES ('SV003', 'K4', 7.5)
INSERT INTO KETQUA
VALUES ('SV001', 'K2', 9.0)
INSERT INTO KETQUA
VALUES ('SV004', 'K3', 6.0)
INSERT INTO KETQUA
VALUES ('SV005', 'K3', 7.0)
INSERT INTO KETQUA
VALUES ('SV002', 'K1', 7.0)
INSERT INTO KETQUA
VALUES ('SV003', 'K2', 8.5)
INSERT INTO KETQUA
VALUES ('SV005', 'K5', 7.0)
INSERT INTO KETQUA
VALUES ('SV004', 'K4', 2.0)

-- CÂU 4.2.1: Cho biết tên, địa chỉ, điện thoại của tất cả các sinh viên.
SELECT TEN, DIACHI, DIENTHOAI FROM SINHVIEN

-- CÂU 4.2.2: Cho biết tên các môn học và số tín chỉ của từng môn học. 
SELECT TENMH, SOTC FROM MONHOC

-- CÂU 4.2.3: Cho biết kết quả học tập của sinh viên có Mã số “SV03”. 
SELECT MAKHOAHOC, DIEM FROM KETQUA WHERE MSSV = 'SV003'

-- CÂU 4.2.4: Cho biết tên các môn học và số tín chỉ của những môn học có cấu trúc của mã môn học như sau: ký tự thứ 1 là “C”, ký tự thứ 3 là “D”. 
SELECT TENMH, SOTC FROM MONHOC WHERE MAMH LIKE 'C_D%'

-- CÂU 4.2.5: Cho biết tên các giáo viên có ký tự thứ 3 là “A”. 
SELECT TENGV FROM GIAOVIEN WHERE TENGV LIKE '__A%'

-- CÂU 4.2.6: Cho biết tên những môn học có chứa chữ “DU” (Ví dụ như các môn Cơ sở dữ liệu, Cấu trúc dữ liệu, …). 
SELECT TENMH FROM MONHOC WHERE TENMH LIKE '%DU%'

-- CÂU 4.2.7: Cho biết tên các giáo viên có ký tự đầu tiên của họ và tên là các ký tự “P” hoặc “L”
SELECT TENGV FROM GIAOVIEN WHERE TENGV LIKE '[PL]%'

-- CÂU 4.2.8: Cho biết tên, địa chỉ của những sinh viên có địa chỉ trên đường “Cống Quỳnh”. 
SELECT TEN, DIACHI FROM SINHVIEN WHERE DIACHI LIKE '%CONG QUYNH%'

-- CÂU 4.2.9: Cho biết danh sách các môn học được dạy trong năm 2002
SELECT MH.MAMH, TENMH
FROM MONHOC MH INNER JOIN GIANGDAY GD ON MH.MAMH = GD.MAMH
WHERE NAM = 2002

-- CÂU 4.2.10: Cho biết mã, tên, địa chỉ của các sinh viên theo từng Khoa sắp theo thứ tự A-Z của tên sinh viên. 
SELECT MSSV, TEN, DIACHI, MAKHOA
FROM SINHVIEN
ORDER BY MAKHOA ASC, TEN ASC

-- CÂU 4.2.11: Cho biết điểm của các sinh viên theo từng môn học.
SELECT MSSV, GD.MAMH, DIEM
FROM KETQUA KQ INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC

-- CÂU 4.2.12: Cho biết các sinh viên học môn ‘CSDL’ có điểm từ 8 đến 10. 
SELECT TEN
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MSSV
				 INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
WHERE MAMH = 'CSDL' AND (DIEM BETWEEN 8 AND 10)

-- CÂU 4.2.13: Cho biết bảng điểm của sinh viên có tên là ‘TUNG’.
SELECT MAMH, DIEM
FROM KETQUA KQ INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
			   INNER JOIN SINHVIEN SV ON KQ.MSSV = SV.MSSV
WHERE TEN LIKE '%TUNG'

-- CÂU 4.2.14: Cho biết tên khoa, tên môn học mà những sinh viên trong khoa đã học. 
SELECT distinct TENKHOA, TENMH
FROM GIANGDAY GD INNER JOIN MONHOC MH ON GD.MAMH = MH.MAMH
				 INNER JOIN KETQUA KQ ON GD.MAKHOAHOC = KQ.MAKHOAHOC
				 INNER JOIN SINHVIEN SV ON KQ.MSSV = SV.MSSV
				 INNER JOIN KHOA KH ON SV.MAKHOA = KH.MAKHOA

-- CÂU 4.2.15: .Cho biết tên khoa, mã khóa học mà giáo viên của khoa có tham gia giảng dạy. 
SELECT TENKHOA, MAKHOAHOC
FROM GIANGDAY GD INNER JOIN GIAOVIEN GV ON GD.MAGV = GV.MAGV
				 INNER JOIN KHOA KH ON GV.MAKHOA = KH.MAKHOA

-- CÂU 4.2.16: Cho biết các sinh viên đã học môn ‘CSDL’ hoặc ‘CTDL’. 
SELECT DISTINCT SV.MSSV, TEN, PHAINU, DIACHI, DIENTHOAI, MAKHOA
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MSSV
				 INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
WHERE MAMH IN ('CSDL', 'CTDL')

-- CÂU 4.2.17: Cho biết tên những giáo viên tham gia giảng dạy môn “Ky Thuat lap trinh”. 
SELECT TENGV
FROM GIAOVIEN GV INNER JOIN GIANGDAY GD ON GV.MAGV = GD.MAGV
				 INNER JOIN MONHOC MH ON GD.MAMH = MH.MAMH
WHERE TENMH = 'KY THUAT LAP TRINH'

-- CÂU 4.2.18: Cho biết tên môn học mà giáo viên “Tran Van Tien” tham gia giảng dạy trong học kỳ 1 năm học 2002. 
SELECT TENMH
FROM MONHOC MH INNER JOIN GIANGDAY GD ON MH.MAMH = GD.MAMH
			   INNER JOIN GIAOVIEN GV ON GD.MAGV = GV.MAGV
WHERE TENGV = 'TRAN VAN TIEN' AND HOCKY = 1 AND NAM = 2002 

-- CÂU 4.2.19: Cho biết mã, tên các sinh viên có kết quả 1 môn học nào đó trên 8 điểm (Kết quả các môn khác có thể <= 8).
SELECT SV.MSSV, TEN
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MSSV
GROUP BY SV.MSSV, TEN
HAVING MAX(DIEM) > 8
	-- HAY
SELECT DISTINCT SV.MSSV, TEN
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MSSV
WHERE SV.MSSV IN (SELECT MSSV FROM KETQUA WHERE DIEM > 8)

-- CÂU 4.2.20: Cho biết mã, tên các sinh viên có kết quả các môn học đều trên 8 điểm
SELECT SV.MSSV, TEN
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MSSV
GROUP BY SV.MSSV, TEN
HAVING MIN(DIEM) > 8

-- CÂU 4.2.21: Có bao nhiêu sinh viên
SELECT COUNT(*) AS [SO SINH VIEN] FROM SINHVIEN

-- CÂU 4.2.22: Có bao nhiêu giáo viên.
SELECT COUNT(*) AS [SO GIAO VIEN] FROM GIAOVIEN

-- CÂU 4.2.23: Có bao nhiêu sinh viên có thuộc tính PhaiNu là Yes và thuộc khoa “CNTT”
SELECT COUNT(*) AS [SO NU THUOC KHOA CNTT] FROM SINHVIEN WHERE PHAINU = 1 AND MAKHOA = 'CNTT'

-- CÂU 4.2.24: Có bao nhiêu giáo viên khoa CNTT. 
SELECT COUNT(*) AS [SO GV KHOA CNTT] FROM GIAOVIEN WHERE MAKHOA = 'CNTT'

-- CÂU 4.2.25: Có bao nhiêu sinh viên học môn CSDL.
SELECT COUNT(*) AS [SO SV HOC CSDL]
FROM KETQUA KQ INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
WHERE MAMH = 'CSDL'

-- CÂU 4.2.26: Có bao nhiêu môn học được giảng dạy trong học kỳ 1 năm 2001.
SELECT COUNT(DISTINCT MAMH) AS [SO MH TRONG HK 1 NAM 2001]
FROM GIANGDAY
WHERE HOCKY = 1 AND NAM = 2001

-- CÂU 4.2.27: Cho biết điểm trung bình của sinh viên có mã số ‘SV004’
SELECT SUM(SOTC * DIEM) / SUM(SOTC) AS [DIEM TB]
FROM KETQUA KQ INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
			   INNER JOIN MONHOC MH ON GD.MAMH = MH.MAMH
WHERE MSSV = 'SV004'

-- CÂU 4.2.28: Cho biết mã, tên, địa chỉ và điểm trung bình của từng sinh viên.
SELECT SV.MSSV, TEN, DIACHI, [DIEM TB]
FROM SINHVIEN SV INNER JOIN (SELECT MSSV, SUM(SOTC * DIEM) / SUM(SOTC) AS [DIEM TB]
						 	 FROM KETQUA KQ INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
											INNER JOIN MONHOC MH ON GD.MAMH = MH.MAMH
							 GROUP BY MSSV) DTB ON SV.MSSV = DTB.MSSV

-- CÂU 4.2.29: Cho biết số lượng điểm >= 8 của từng sinh viên.
SELECT MSSV, COUNT(*) AS [SO DIEM >= 8]
FROM KETQUA
WHERE DIEM >= 8
GROUP BY MSSV

-- CÂU 4.2.30: Cho biết tên khoa, số lượng sinh viên có trong từng khoa.
SELECT TENKHOA, SL AS [SO LUONG SINH VIEN]
FROM KHOA KH INNER JOIN (SELECT MAKHOA, COUNT(MSSV) AS [SL]
			 FROM SINHVIEN GROUP BY MAKHOA) SLSV ON KH.MAKHOA = SLSV.MAKHOA

-- CÂU 4.2.31: Cho biết tên khoa, số lượng khóa học mà giáo viên của khoa có tham gia giảng dạy.
SELECT TENKHOA, COUNT(MAKHOAHOC) [SO LUONG KHOA HOC]
FROM KHOA KH INNER JOIN GIAOVIEN GV ON KH.MAKHOA = GV.MAKHOA
			 INNER JOIN GIANGDAY GD ON GV.MAGV = GD.MAGV
GROUP BY TENKHOA

-- CÂU 4.2.32: Cho biết tên tất cả các sinh viên, điểm trung bình, số lượng khóa học đã tham gia học tập.
SELECT TEN, [DIEM TRUNG BINH], [TONG KHOA HOC]
FROM SINHVIEN SV INNER JOIN (SELECT MSSV, SUM(SOTC * DIEM) / SUM(SOTC) AS [DIEM TRUNG BINH], COUNT(KETQUA.MAKHOAHOC) AS [TONG KHOA HOC]
							 FROM KETQUA INNER JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC
										 INNER JOIN MONHOC ON GIANGDAY.MAMH = MONHOC.MAMH
							 GROUP BY MSSV) TK ON SV.MSSV = TK.MSSV

-- CÂU 4.2.33: Cho biết số lượng tín chỉ mà từng sinh viên đã tham gia (gồm MSSV, tên SV, Số lượng tín chỉ)
SELECT SV.MSSV, TEN, SUM(SOTC) AS [TONG SO TIN CHI]
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MaSV
				 INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
				 INNER JOIN MONHOC MH ON GD.MAMH = MH.MAMH
GROUP BY SV.MSSV, TEN

-- CÂU 4.2.34: Cho biết tên những sinh viên chỉ mới thi đúng một môn.
SELECT TEN
FROM SINHVIEN
WHERE MSSV IN (SELECT MSSV
				FROM KETQUA
				GROUP BY MSSV
				HAVING COUNT(MSSV) = 1)

-- CÂU 4.2.35: Cho biết mã, tên, địa chỉ và điểm của các sinh viên có điểm trung bình > 8.5 
SELECT SV.MSSV, TEN, DIACHI, [DIEM TB]
FROM SINHVIEN SV INNER JOIN (SELECT MaSV, SUM(SOTC * DIEM) / SUM(SOTC) AS [DIEM TB]
							 FROM KETQUA INNER JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC
										 INNER JOIN MONHOC ON GIANGDAY.MAMH = MONHOC.MAMH
						   	 GROUP BY MaSV
							 HAVING SUM(SoTC * Diem) / SUM(SoTC) > 8.5) TK ON SV.MSSV = TK.MaSV

-- CÂU 4.2.36: Cho biết Mã khóa học, học kỳ, năm, số lượng sinh viên tham gia của những khóa học có số lượng sinh viên tham gia (đã có điểm) từ 2 đến 4 người.
SELECT GD.MAKHOAHOC, HOCKY, NAM, COUNT(MSSV) AS [SO SV HOC]
FROM GIANGDAY GD INNER JOIN KETQUA KQ ON GD.MAKHOAHOC = KQ.MAKHOAHOC
GROUP BY GD.MAKHOAHOC, HOCKY, NAM
HAVING COUNT(MSSV) BETWEEN 2 AND 4

-- CÂU 4.2.37: Cho biết các sinh viên học cả 2 môn ‘CSDL’ và ‘CTDL’ học có điểm của 1 trong 2 môn >= 8
SELECT MSSV
FROM KETQUA KQ INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
WHERE MAMH IN ('CSDL', 'CTDL') AND DIEM >= 8
GROUP BY MSSV
HAVING COUNT(DISTINCT MAMH) >= 2

-- CÂU 4.2.38: Điểm cao nhất mà sinh viên đã đạt được trong các khóa học.
SELECT MAX(DIEM) AS [DIEM CAO NHAT]
FROM KETQUA

-- CÂU 4.2.39: Trong các môn học, số tín chỉ nhỏ nhất là bao nhiêu? 
SELECT MIN(SOTC) AS [SOTC NHO NHAT]
FROM MONHOC

-- CÂU 4.2.40: Cho biết tên của môn học có số tín chỉ nhiều nhất.
SELECT TENMH
FROM MONHOC
WHERE SOTC >= (SELECT MAX(SOTC) FROM MONHOC)

-- CÂU 4.2.41: Cho biết tên của khoa có số lượng CBGD ít nhất.
SELECT TENKHOA
FROM KHOA
WHERE SL_CBGD <= (SELECT MIN(SL_CBGD) FROM KHOA)

-- CÂU 4.2.42: Tên các sinh viên có điểm cao nhất trong môn ‘Kỹ Thuật Lập Trình’
SELECT SV.MSSV, TEN, DIEM
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MSSV
				 INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
				 INNER JOIN MONHOC MH ON GD.MAMH = MH.MAMH
WHERE TENMH = 'KY THUAT LAP TRINH'
	  AND DIEM >= ALL (SELECT DIEM
					   FROM KETQUA INNER JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC
					   INNER JOIN MONHOC ON GIANGDAY.MAMH = MONHOC.MAMH)
	-- HAY
SELECT TEN
FROM SINHVIEN
WHERE MSSV IN (SELECT MSSV
				FROM KETQUA INNER JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC
							INNER JOIN MONHOC ON GIANGDAY.MAMH = MONHOC.MAMH
				WHERE  DIEM >= ALL (SELECT DIEM
									FROM KETQUA KQ INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
												   INNER JOIN MONHOC MH ON GD.MAMH = MH.MAMH
									WHERE TENMH = 'KY THUAT LAP TRINH')
						AND TENMH = 'KY THUAT LAP TRINH')

-- CÂU 4.2.43: Cho biết mã, tên, địa chỉ của các sinh viên có điểm thi môn CSDL lớn nhất.
SELECT SV.MSSV, TEN, DIACHI
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MSSV
				 INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
WHERE MAMH = 'CSDL' AND DIEM >= (SELECT MAX(DIEM) FROM KETQUA INNER JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC WHERE MAMH = 'CSDL')

-- CÂU 4.2.44: Cho biết tên các môn học có nhiều sinh viên tham gia nhất (tên môn, số lượng sinh viên).
SELECT MAMH, COUNT(MSSV) AS [SO SV HOC]
FROM GIANGDAY GD INNER JOIN KETQUA KQ ON GD.MAKHOAHOC = KQ.MAKHOAHOC
GROUP BY MAMH
HAVING COUNT(MSSV) >= ALL (SELECT COUNT(MSSV)
						   FROM GIANGDAY INNER JOIN KETQUA ON GIANGDAY.MAKHOAHOC = KETQUA.MAKHOAHOC
						   GROUP BY MAMH)

-- CÂU 4.2.45: Đối với mỗi môn học, cho biết tên và điểm của các sinh viên có điểm cao nhất.
SELECT MH.MAMH, TENMH, TEN, DIEM
FROM MONHOC MH INNER JOIN GIANGDAY GD ON MH.MAMH = GD.MAMH
			   INNER JOIN KETQUA KQ ON GD.MAKHOAHOC = KQ.MAKHOAHOC
			   INNER JOIN SINHVIEN SV ON KQ.MSSV = SV.MSSV
WHERE DIEM >= (SELECT MAX(DIEM) FROM KETQUA INNER JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC WHERE MAMH = MH.MAMH)

-- CÂU 4.2.46: Học kỳ nào có nhiều môn học được giảng dạy nhất (không quan tâm đến năm học).
SELECT HOCKY, COUNT(MAMH) AS [SO MON HOC DUOC GIANG DAY]
FROM GIANGDAY
GROUP BY HOCKY
HAVING COUNT(MAMH) >= ALL (SELECT COUNT(MAMH) FROM GIANGDAY GROUP BY HOCKY)

-- CÂU 4.2.47: Cho biết tên các sinh viên có nhiều điểm 7 nhất (bao gồm tên sinh viên, số lượng điểm 7).
SELECT KQ.MSSV, TEN, COUNT(DIEM) AS [SO DIEM 7]
FROM KETQUA KQ INNER JOIN SINHVIEN SV ON KQ.MSSV = SV.MSSV
WHERE DIEM = 7
GROUP BY KQ.MSSV, TEN
HAVING COUNT(DIEM) >= ALL (SELECT COUNT(DIEM) FROM KETQUA WHERE DIEM = 7 GROUP BY MSSV)

-- CÂU 4.2.48: Cho biết tên các sinh viên có số lượng tín chỉ nhiều nhất (bao gồm tên sinh viên, số lượng tín chỉ đã tham gia).
SELECT SV.MSSV, TEN, SUM(SOTC)  AS [TONG TIN CHI]
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MSSV
				 INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
				 INNER JOIN MONHOC MH ON GD.MAMH = MH.MAMH
GROUP BY SV.MSSV, TEN
HAVING SUM(SOTC) >= ALL (SELECT SUM(SOTC) FROM KETQUA INNER JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC
													  INNER JOIN MONHOC ON GIANGDAY.MAMH = MONHOC.MAMH
						 GROUP BY MSSV)

-- CÂU 4.2.49: Cho biết tên môn học, tên sinh viên, điểm của các sinh viên học những môn học có số tín chỉ là thấp nhất. 
SELECT TENMH, TEN, DIEM
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MSSV
				 INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
				 INNER JOIN (SELECT MAMH, TENMH FROM MONHOC WHERE SOTC <= ALL (SELECT SOTC FROM MONHOC)) MH ON GD.MAMH = MH.MAMH

	-- HAY
SELECT TENMH, TEN, DIEM
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MSSV
				 INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
				 INNER JOIN MONHOC MH ON GD.MAMH = MH.MAMH 
WHERE SOTC <= (SELECT MIN(SOTC) FROM MONHOC)

-- CÂU 4.2.50: Cho biết tên những giáo viên tham gia giảng dạy nhiều nhất
SELECT GV.MAGV, TENGV, COUNT(MAKHOAHOC) AS [SO KHOA HOC GIANG DAY]
FROM GIANGDAY GD INNER JOIN GIAOVIEN GV ON GD.MAGV = GV.MAGV
GROUP BY GV.MAGV, TENGV
HAVING COUNT(MAKHOAHOC) >= ALL (SELECT COUNT(MAKHOAHOC) FROM GIANGDAY GROUP BY MAGV)

-- CÂU 4.2.51: Tên các giáo viên không tham gia giảng dạy trong năm 2001.
SELECT MAGV, TENGV
FROM GIAOVIEN
WHERE MAGV NOT IN (SELECT DISTINCT MAGV FROM GIANGDAY WHERE Nam = 2001)


-- CÂU 4.2.52: Cho biết tên các môn học không được tổ chức trong năm 2001.
SELECT TENMH
FROM MONHOC
WHERE MAMH NOT IN (SELECT MAMH FROM GIANGDAY WHERE NAM = 2001)


-- CÂU 4.2.53: Tên những khoa chưa có sinh viên theo học. 
SELECT TENKHOA
FROM KHOA 
WHERE MAKHOA NOT IN (SELECT DISTINCT MAKHOA FROM SINHVIEN)

-- CÂU 4.2.54: Cho biết tên những môn học chưa được tổ chức cho các khóa học
SELECT TENMH
FROM MONHOC
WHERE MAMH NOT IN (SELECT DISTINCT MAMH FROM GIANGDAY)



-- CÂU 4.2.55: Cho biết tên những sinh viên chưa có điểm kiểm trahqtcsdl
SELECT DISTINCT TEN
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MSSV
WHERE DIEM IS NULL

-- CÂU 4.2.56: Cho biết tên những khoa không có sinh viên theo học
SELECT TENKHOA
FROM KHOA
WHERE MAKHOA NOT IN (SELECT DISTINCT MAKHOA FROM SINHVIEN)

-- CÂU 4.2.57: Tượng tự, cho biết tên sinh viên, số lượng môn mà sinh viên chưa học
SELECT MSSV, TEN, COUNT(MAMH) AS [SO MON CHUA HOC]
FROM SINHVIEN SV, MONHOC
WHERE MAMH NOT IN (SELECT MAMH FROM KETQUA INNER JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC
				   WHERE MSSV = SV.MSSV)
GROUP BY MSSV, TEN

-- CÂU 4.2.58: Cho biết các sinh viên chưa học môn ‘LTC trên Windows’
SELECT MSSV, TEN
FROM SINHVIEN
WHERE MSSV NOT IN (SELECT MSSV
				   FROM KETQUA KQ INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
								  INNER JOIN MONHOC MH ON GD.MAMH = MH.MAMH
				   WHERE TENMH = 'LAP TRINH C TREN WINDOW')

-- CÂU 4.2.59: Cho biết tên tất cả các giáo viên cùng với số lương khóa học mà từng giáo viên đã tham gia giảng dạy. 
SELECT GV.MAGV, TENGV, COUNT(MAKHOAHOC) AS [SO KHOA HOC DA DAY]
FROM GIAOVIEN GV LEFT JOIN GIANGDAY GD ON GV.MAGV = GD.MAGV
GROUP BY GV.MAGV, TENGV

--HỢP/GIAO/TRỪ

-- CÂU 4.2.60: Giả sử quy định mỗi sinh viên phải học đủ tất cả các môn học. Cho biết tên sinh viên, tên môn mà sinh viên chưa học
SELECT MSSV, TEN, TENMH FROM SINHVIEN SV, MONHOC
EXCEPT
SELECT SINHVIEN.MSSV, TEN, TENMH
FROM SINHVIEN INNER JOIN KETQUA ON SINHVIEN.MSSV = KETQUA.MSSV
			  INNER JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC
			  INNER JOIN MONHOC ON GIANGDAY.MAMH = MONHOC.MAMH


-- CÂU 4.2.61: Cho biết tên những giáo viên tham gia dạy đủ tất cả các môn học
SELECT MAGV, TENGV
FROM GIAOVIEN GV
WHERE NOT EXISTS (SELECT MAMH FROM MONHOC EXCEPT SELECT DISTINCT MAMH FROM GIANGDAY WHERE MAGV = GV.MAGV)

-- CÂU 4.2.62: Cho biết tên những môn học mà tất cả các giáo viên đều tham gia giảng dạy.
SELECT MAMH, TENMH
FROM MONHOC MH
WHERE NOT EXISTS (SELECT MAGV FROM GIAOVIEN EXCEPT SELECT DISTINCT MAGV FROM GIANGDAY WHERE MAMH = MH.MAMH)

-- CÂU 4.2.63: Cho biết khóa học mà tất cả các sinh viên đều tham gia.
SELECT MAKHOAHOC
FROM GIANGDAY GD
WHERE NOT EXISTS (SELECT MSSV FROM SINHVIEN EXCEPT SELECT DISTINCT MSSV FROM KETQUA WHERE MAKHOAHOC = GD.MAKHOAHOC)

-- CÂU 4.2.64: Cho biết tên những sinh viên tham gia đủ tất cả các khóa học.
SELECT MSSV, TEN
FROM SINHVIEN SV
WHERE NOT EXISTS (SELECT MAKHOAHOC FROM GIANGDAY EXCEPT SELECT DISTINCT MAKHOAHOC FROM KETQUA WHERE MSSV = SV.MSSV)

-- CÂU 4.2.65: Cho biết tên môn học mà tất cả các sinh viên đều đã học.
SELECT MAMH, TENMH
FROM MONHOC MH
WHERE NOT EXISTS (SELECT MSSV FROM SINHVIEN
				  EXCEPT
				  SELECT DISTINCT MSSV FROM KETQUA JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC WHERE MAMH = MH.MAMH)

-- CÂU 4.2.66: Cho biết tên sinh viên đã học đủ tất cả các môn học.
SELECT MSSV, TEN
FROM SINHVIEN SV
WHERE NOT EXISTS (SELECT MAMH FROM MONHOC
				  EXCEPT
				  SELECT DISTINCT MAMH FROM KETQUA JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC WHERE MSSV = SV.MSSV)

-- CÂU 4.2.67: Cho biết tên những sinh viên đã học tất cả những môn mà sinh viên ‘SV001’ đã học
SELECT SV.MSSV, TEN
FROM SINHVIEN SV INNER JOIN KETQUA KQ ON SV.MSSV = KQ.MaSV
				 INNER JOIN GIANGDAY GD ON KQ.MAKHOAHOC = GD.MAKHOAHOC
WHERE SV.MSSV != 'SV001'
	  AND MAMH = ANY (SELECT MAMH
					  FROM GIANGDAY INNER JOIN KETQUA ON GIANGDAY.MAKHOAHOC = KETQUA.MAKHOAHOC
					  WHERE MaSV = 'SV001')
GROUP BY SV.MSSV, TEN
HAVING COUNT(DISTINCT MAMH) = (SELECT COUNT(DISTINCT MAMH)
							   FROM GIANGDAY INNER JOIN KETQUA ON GIANGDAY.MAKHOAHOC = KETQUA.MAKHOAHOC
							   WHERE MaSV = 'SV001')

--CÂU 4.2.68. Cho biết tên những giáo viên đã dạy tất cả những môn học mà giáo viên ‘GV03’ đã dạy. 
SELECT GV.MAGV, TENGV
FROM GIAOVIEN GV INNER JOIN GIANGDAY GD ON GV.MAGV = GD.MAGV
WHERE GV.MAGV != 'GV03'
	  AND MAMH = ANY (SELECT MAMH FROM GIANGDAY WHERE MAGV = 'GV03')
GROUP BY  GV.MAGV, TENGV
HAVING COUNT(DISTINCT MAMH) = (SELECT COUNT(DISTINCT MAMH) FROM GIANGDAY WHERE MAGV = 'GV03')

SELECT * FROM dbo.SINHVIEN
--CÂU 4.2.69: Thêm các field SLMon (số lượng môn), DTB (điểm trung bình), XL (xếp loại) vào table SinhVien
ALTER TABLE SINHVIEN
ADD SLMON INT, DTB REAL, XL NVARCHAR(20)
--CÂU 4.2.70-71: Cập nhật thông tin cho các field vừa tạo theo yêu cầu:
UPDATE SINHVIEN
SET SLMON = (SELECT COUNT(DISTINCT MAMH) FROM KETQUA JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC WHERE MSSV = SINHVIEN.MSSV)
UPDATE SINHVIEN
SET DTB = (SELECT SUM(DIEM * SOTC) / SUM(SOTC)
		   FROM KETQUA JOIN GIANGDAY ON KETQUA.MAKHOAHOC = GIANGDAY.MAKHOAHOC
					   JOIN MONHOC ON GIANGDAY.MAMH = MONHOC.MAMH
		   WHERE MSSV = SINHVIEN.MSSV)
UPDATE SINHVIEN
SET XL = CASE
			WHEN DTB < 5 THEN 'YEU'
			WHEN DTB < 6.5 THEN 'TRUNG BINH'
			WHEN DTB < 8 THEN 'KHA'
			WHEN DTB < 9 THEN 'GIOI'
			ELSE 'XUAT SAC'
		 END

--CÂU 4.2.72: Xóa tất cả kết quả học tập của sinh viên ‘SV002’
DELETE FROM KETQUA WHERE MSSV = 'SV002'

--CÂU 4.2.73: Xóa tên những sinh viên có điểm trung bình dưới 5
DELETE FROM SINHVIEN WHERE DTB < 5

--CÂU 4.2.74: Xóa những khoa không có sinh viên theo học
DELETE FROM KHOA
WHERE MAKHOA NOT IN (SELECT DISTINCT MAKHOA FROM SINHVIEN)












